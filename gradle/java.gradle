apply plugin: "java-library"
apply plugin: "maven-publish"

sourceCompatibility = 1.8
targetCompatibility = 1.8

// Build Source Jars
def sourcesJarMap = [name: 'sourcesJar', type: Jar, dependsOn: classes]
project.tasks.create(sourcesJarMap, {
    classifier = 'sources'
    from sourceSets.main.allSource
})

// Build Javadoc Jars
def javadocJarMap = [name: 'javadocJar', type: Jar, dependsOn: javadoc]
project.tasks.create(javadocJarMap, {
    classifier = 'javadoc'
    from javadoc.destinationDir
})

// Build Artifacts
artifacts {
    archives sourcesJar
    archives javadocJar
}

jar {
    archiveVersion = "${project.version}"
}

// Artifacts to Publish
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = project.name
                description = 'Utility for parsing environment variable into system parameters.'
                url = 'https://github.com/gregwhitaker/envopts'
                version = project.version

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id = 'gregwhitaker'
                        name = 'Greg Whitaker'
                        email = 'gregory.a.whitaker@gmail.com'
                        url = 'https://github.com/gregwhitaker'
                    }
                }

                scm {
                    url = 'https://github.com/gregwhitaker/envopts'
                    connection = 'scm:git:git://github.com/gregwhitaker/envopts.git'
                    developerConnection = 'scm:git:git@github.com:gregwhitaker/envopts.git'
                }
            }
        }
    }
}